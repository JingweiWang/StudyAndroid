apply plugin: 'com.android.application'
android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "io.github.jingweiwang.gradleforandroid"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        rwtest.initWith(buildTypes.debug)
        rwtest {
            applicationIdSuffix ".rwtest"
            versionNameSuffix "-rwtest"
        }
    }
    productFlavors {
        flavorDimensions "store", "price"
        tencent {
            applicationIdSuffix ".tencent"
            versionNameSuffix "-tencent"
            dimension "store"
        }
        wandoujia {
            applicationIdSuffix ".wandoujia"
            versionNameSuffix "-wandoujia"
            dimension "store"
        }
        free {
            dimension "price"
        }
        paid {
            dimension "price"
        }
    }
    variantFilter {
        variant ->
            if (variant.buildType.name == 'rwtest') {
                variant.getFlavors().each() {
                    flavor ->
                        if (flavor.name == 'free') {
                            variant.setIgnore(true);
                        }
                }
            }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.0'
    wearApp project(':GradleForAndroid:wearapplication')
}

task hello {
    doLast {
        println 'hello,world.'
    }
}

task openSetting {
    doLast {
        exec {
            def adb = android.getAdbExecutable().toString()
            executable = "$adb"
            args = ['shell', 'am', 'start', '-n', 'com.android.settings/com.android.settings.Settings']
        }
    }
}